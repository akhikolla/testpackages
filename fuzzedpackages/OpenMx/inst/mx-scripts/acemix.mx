! Mixture distribution model for MZ & DZ misclassification

G1: Model parameters
Data Calc NGroups=3
Begin Matrices;
H Full 1 1
Q Full 1 1
M Full 1 1 Free
X Lower 1 1 Free
Y Lower 1 1 Free
Z Lower 1 1 Free
W Lower 1 1 
End Matrices;
! parameters are fixed by default, unless declared free
Matrix H .5
Matrix Q .25
Start .6 All

Begin Algebra;
A= X*X';
C= Y*Y';
E= Z*Z';
D= W*W';
N=M|M_M|M;
End Algebra:
End

G2: MZ twin pairs
Data NInput_vars=3 Nmodel=2
Labels t1 t2 r
Rectangular File=sim1.mz
Select t1 t2;

Matrices= Group 1
P Full 2 1
End Matrices;
Matrix P .95 .05   ! probabilities of being MZ and DZ, respectively

Means N;
Covariances A+C+D+E | A+C+D _
            A+C+D   | A+C+D+E _
            A+C+D+E   | H@A+C+Q@D _
            H@A+C+Q@D | A+C+D+E ;
Weights P;
Options RSidual
End 

G3: DZ twin pairs
Data NInput_vars=3 Nmodel=2
Labels t1 t2 r
Rectangular File=sim1.dz
Select t1 t2;

Matrices= Group 1
P Full 2 1
End Matrices;

Matrix P .95 .05   ! probabilities of being DZ and MZ, respectively
Means N;
Covariances A+C+D+E   | H@A+C+Q@D _
            H@A+C+Q@D | A+C+D+E _
            A+C+D+E | A+C+D _
            A+C+D   | A+C+D+E ;
Weights P;

Intervals a 1 1 1 c 1 1 1 e 1 1 1
Options RSidual NDecimals=4
End
