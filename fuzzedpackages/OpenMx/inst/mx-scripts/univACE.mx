! Estimate genetic and environmental components - ACED model
! OZ BMI data - younger females

#NGroups 4
#define nvar 1
#define nvar2 2

Title G1: Model Parameters
 Calculation
 Begin Matrices;
  X Lower nvar nvar Free  ! additive genetic path, a
  Y Lower nvar nvar Free      ! common environmental path, c
  Z Lower nvar nvar Free  ! specific environmental path, e
  W Lower nvar nvar !Free  ! dominance genetic path, d
  H Full 1 1              ! scalar, 0.5
  Q Full 1 1              ! scalar, 0.25
 End Matrices;
  Label Row X add_gen
  Label Row Y comm_env
  Label Row Z spec_env
  Label Row W dom_gen
  Matrix H .5
  Matrix Q .25
 Begin Algebra;
  A= X*X';                ! additive genetic variance, a^2
  C= Y*Y';                ! common environmental variance, c^2
  E= Z*Z';                ! specific environmental variance, e^2
  D= W*W';                ! dominance genetic variance, d^2
 End Algebra;
End Group

Title G2: MZ data
 #include ozbmi.dat
 Select if zyg = 1
 Select bmi1 bmi2 ;
 Begin Matrices = Group 1;
  M Full 1 nvar2 Free
 End Matrices;
  Specify M 10 10
 Means M;
 Covariance 
    A+C+E+D | A+C+D   _  
    A+C+D   | A+C+E+D;
 Option RSiduals;
End

Title G3: DZ data
 #include ozbmi.dat
 Select if zyg = 3
 Select bmi1 bmi2 ;
 Begin Matrices = Group 1;
  M Full 1 nvar2 Free
 End Matrices;
  Specify M 10 10
 Means M;
 Covariance
    A+C+E+D   | H@A+C+Q@D _
    H@A+C+Q@D | A+C+E+D;
 Option RSiduals
End

Title G4: Standardization
 Calculation
 Begin Matrices = Group 1;
  G Full 1 1 =%F2
  H Full 1 1 =%F3
 End Matrices;
  Start .6 all
  Start 20 M 2 1 1 - M 2 1 nvar2
  Start 20 M 3 1 1 - M 3 1 nvar2
 Begin Algebra;
  F=G+H;
  V=A+C+E+D;              ! total variance
  P=A|C|E|D;              ! concatenate parameter estimates
  S=P@V~;                 ! standardized parameter estimates
 End Algebra;
!ADE model
! Interval S 1 1 - S 1 4
 Option NDecimals=4
 Option Sat=4055.935,1767 ! from ozbmifysat.mxo
 Option MxF=LLACE.mxm
End
